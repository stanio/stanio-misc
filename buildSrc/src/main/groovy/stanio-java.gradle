plugins {
    id 'java'
    id 'eclipse'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs.add '-Xlint:all'
    options.compilerArgs.add '-Xdoclint:-missing'
}

tasks.named('test') {
    useJUnitPlatform()
}

javadoc {
    //failOnError false
    options.encoding = 'UTF-8'
    options.addBooleanOption('Xdoclint:all,-missing', true)
    options.addStringOption('-release', '21')
    options.tags 'min.jdk:C:Minimum Java version:'
}

eclipse {
    classpath {
        def baseOutputDir = '.classes'
        defaultOutputDir = file("${baseOutputDir}/default")

        // This is ridiculous:
        // https://github.com/gradle/gradle/issues/3839
        file.whenMerged {
            entries.each { entry ->
                if (entry instanceof org.gradle.plugins.ide.eclipse.model.SourceFolder) {
                    entry.output = entry.output.replaceFirst('^bin(?=/)', baseOutputDir)
                }
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.assertj:assertj-core:3.27.3'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.11.4'
    testRuntimeOnly    'org.junit.platform:junit-platform-launcher'
}
