plugins {
    id 'stanio-java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

application {
    mainClass = 'io.github.stanio.bibata.Command'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    //toolchain {
    //    languageVersion = JavaLanguageVersion.of(17)
    //}
}

tasks.withType(JavaCompile) {
    options.release = 11
}

jar {
    manifest {
        attributes('Main-Class': application.mainClass)
    }
}

shadowJar {
    archiveBaseName = 'bibata'
    archiveClassifier = ''
    exclude 'io/github/stanio/bibata/BitmapsRenderer*'
    exclude 'org/apache/batik/**/*'
    minimize {
        exclude dependency('com.google.code.gson:gson')
        exclude dependency('com.github.weisj:jsvg')
    }
    dependencies {
        exclude dependency('org.apache.xmlgraphics:.*')
        exclude dependency('xml-apis:xml-apis-ext')
        exclude dependency('commons-.*:commons-.*')
    }
}

repositories {
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}

dependencies {
    implementation rootProject
    implementation 'com.google.code.gson:gson:2.10.1'
    // XXX: https://github.com/weisJ/jsvg/issues/59
    implementation 'com.github.weisj:jsvg:latest.integration' // 1.4.0-SNAPSHOT
}
