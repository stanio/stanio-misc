plugins {
    id 'stanio-java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

application {
    mainClass = 'io.github.stanio.bibata.Command'
    def argStr = bibataJvmArgs.trim()
    applicationDefaultJvmArgs = argStr ? argStr.split(' ') as List
                                       : []
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    //toolchain {
    //    languageVersion = JavaLanguageVersion.of(17)
    //}
}

tasks.withType(JavaCompile) {
    options.release = 11
}

apply from: 'package.gradle'

task x11Symlinks(type: JavaExec) {
    description = 'Creates preconfigured XCursor symlinks'
    group = 'package'

    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'io.github.stanio.bibata.config.X11Symlinks'
}

task colorWheel(type: JavaExec) {
    description = 'Generates/updates animated cursor frame static images'
    group = 'config'

    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'io.github.stanio.bibata.source.ColorWheel'
}

task cursorHotspots(type: JavaExec) {
    description = 'Inserts/updates cursor hotspots from build(.toml).json into the SVG sources'
    group = 'config'

    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'io.github.stanio.bibata.source.CursorHotspots'
}

jar {
    manifest {
        attributes('Main-Class': application.mainClass)
    }
}

shadowJar {
    archiveBaseName = 'bibata'
    archiveClassifier = ''
    minimize {
        exclude dependency('com.google.code.gson:gson')
        exclude dependency('com.github.weisj:jsvg')
        exclude dependency('com.fasterxml.woodstox:woodstox-core')
        exclude dependency('org.apache.xmlgraphics:.*')
    }
}

//repositories {
//    maven {
//        // Test with JSVG nightly snapshots
//        url 'https://oss.sonatype.org/content/repositories/snapshots/'
//    }
//}

dependencies {
    implementation rootProject
    implementation 'com.google.code.gson:gson:2.10.1'
    //implementation 'com.github.weisj:jsvg:latest.integration'
    implementation 'com.github.weisj:jsvg:1.5.0'
    implementation 'com.jhlabs:filters:2.0.235-1'
    runtimeOnly    'com.fasterxml.woodstox:woodstox-core:6.6.2'
}
