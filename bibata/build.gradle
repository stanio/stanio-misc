plugins {
    id 'stanio-java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

application {
    mainClass = 'io.github.stanio.bibata.Command'
    applicationDefaultJvmArgs = bibataJvmArgs.split(' ') as List
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    //toolchain {
    //    languageVersion = JavaLanguageVersion.of(17)
    //}
}

tasks.withType(JavaCompile) {
    options.release = 11
}

task colorWheel(type: JavaExec) {
    description = 'Generates/updates animated cursor frame static images'
    group = 'config'

    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'io.github.stanio.bibata.config.ColorWheel'
}

task cursorHotspots(type: JavaExec) {
    description = 'Inserts/updates cursor hotspots from build(.toml).json into the SVG sources'
    group = 'config'

    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'io.github.stanio.bibata.config.CursorHotspots'
}

jar {
    manifest {
        attributes('Main-Class': application.mainClass)
    }
}

shadowJar {
    archiveBaseName = 'bibata'
    archiveClassifier = ''
    exclude 'io/github/stanio/bibata/BatikRendererBackend*'
    exclude 'org/apache/batik/**/*'
    minimize {
        exclude dependency('com.google.code.gson:gson')
        exclude dependency('com.github.weisj:jsvg')
        exclude dependency('com.fasterxml.woodstox:woodstox-core')
    }
    dependencies {
        exclude dependency('org.apache.xmlgraphics:.*')
        exclude dependency('xml-apis:xml-apis-ext')
        exclude dependency('commons-.*:commons-.*')
    }
}

repositories {
    maven {
        // Test with JSVG nightly snapshots
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}

dependencies {
    implementation rootProject
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.github.weisj:jsvg:latest.integration' // 1.5.0-SNAPSHOT
    implementation 'com.jhlabs:filters:2.0.235-1'
    runtimeOnly    'com.fasterxml.woodstox:woodstox-core:6.6.0'
}
