ext {
    theme = [
        shapes : [
            Modern : 'rounded edge',
            Original : 'sharp edge'
        ],
        colors : [
            Amber : 'Yellowish',
            Classic : 'Black',
            Ice : 'White',
            DarkRed : 'Rich red',
            DodgerBlue : 'Slightly light blue',
            Pink : 'Queen pink',
            Turquoise : 'Sky blue with green tint'
        ]
    ]
}

def themeComment(Map args) {
   def comment = "${theme.colors[args.color]} and ${theme.shapes[args.shape]}"
   if (args.leftHand) comment += ', left-handed'
   if (args.outline) comment += ', thin-outline'
   if (args.shadow) comment += ' with pointer shadow'
   //if (args.sizing) comment += " (${ sizing.toLowerCase() })"
   return comment + ' Bibata cursors.'
}

def themeName(Map args) {
   def name = "Bibata ${args.shape} ${args.color}"
   def tags = [ args.leftHand ? 'left-handed' : null,
                args.outline ? 'thin' : null,
                args.shadow ? 'w/ shadow' : null,
                args.sizing ? args.sizing.toLowerCase() : null ] - null
   return tags.empty ? name
                     : name + " (${ tags.join(', ').replace(', w/', ' w/') })"
}

task windowsInstallScripts {
    description = 'install.inf, uninstall.cmd'
    group = 'package'

    doLast {
        ['Modern', 'Original'].each { shape ->
            ['Classic', 'Ice'].each { color ->
                def themeIndex = 1
                def setupInfo = [
                    fileSets: [:],
                    destinationDirs: [],
                    schemesReg: [],
                    strings: []
                ]
                [/*'Regular'*/ null, 'Large', 'Extra-Large'].each { schemeSize ->
                    def args = [ shape: shape,
                                 color: color,
                                 leftHand: null,
                                 outline: null,
                                 shadow: null,
                                 sizing: schemeSize ]

                    addSetupInfo(args, themeIndex++, setupInfo)
                    addSetupInfo(args << [outline: 'Thin'], themeIndex++, setupInfo)
                    if (schemeSize == 'Extra-Large') return // no Extra-Large with Shadow

                    addSetupInfo(args << [shadow: 'Shadow'], themeIndex++, setupInfo)
                    addSetupInfo(args << [outline: null], themeIndex++, setupInfo)
                }
                writeWindowsInstallScripts(setupInfo)
            }
        }
    }
}

def writeWindowsInstallScripts(Map setupInfo) {
    def properties = [
        name: setupInfo.name,
        comment: setupInfo.comment,
        version: "v${cursorsVersion}",
        copyFiles: setupInfo.fileSets.keySet().join(','),
        destinationDirs: setupInfo.destinationDirs.join('\n'),
        schemesReg: setupInfo.schemesReg.join('\n'),
        sourceFiles: setupInfo.fileSets.values().join('\n\n'),
        strings: setupInfo.strings.join('\n')
    ]

    copy {
        into "${buildDir}/windows-scripts"
        from('src/package') {
            include 'install.inf'
            include 'uninstall.cmd'
        }
        rename {
            it.replace('install',
                    "install-${ properties.name.replace(' ', '-') }")
        }
        expand(properties) {
            escapeBackslash = true
        }
    }
}

def addSetupInfo(Map args, index, setupInfo) {
    def name = themeName(args)
    if (!setupInfo.name) {
        setupInfo.name = name
        setupInfo.comment = themeComment(args)
    }

    def curDir = winCurDir(args)
    def srcDir = curDir.replace('Bibata\\', '')
    def fileList = [ 'Pointer.cur',
                     'Help.cur',
                     'Work.ani',
                     'Busy.ani',
                     'Cross.cur',
                     'Text.cur',
                     'Handwriting.cur',
                     'Unavailable.cur',
                     'Vert.cur',
                     'Horz.cur',
                     'Dgn1.cur',
                     'Dgn2.cur',
                     'Move.cur',
                     'Alternate.cur',
                     'Link.cur',
                     'Pin.cur',
                     'Person.cur',
                     'Pan.cur',
                     'Grabbing.cur',
                     'Zoom-in.cur',
                     'Zoom-out.cur',
                     'Alternate_2.cur',
                     'Alternate_3.cur',
                     'Cross_2.cur',
                     'Unavailable_2.cur' ].collect {
        "${srcDir}\\${it}"
    }

    def fileSetName = "Scheme.${index == 1 ? 'Cur' : index}"
    def curDirVar = "CUR_DIR${index == 1 ? '' : index}"
    def schemeNameVar = "SCHEME_NAME${index == 1 ? '' : index}"

    setupInfo.fileSets[fileSetName] = "[${fileSetName}]\n${ fileList.join('\n') }"
    setupInfo.destinationDirs << "${fileSetName} = 10,\"%BIBATA_DIR%\""
    setupInfo.schemesReg << "HKCU,\"Control Panel\\Cursors\\Schemes\",\"%${schemeNameVar}%\",0x00020000,\"%BD%\\%${curDirVar}%\\%pointer%,%BD%\\%${curDirVar}%\\%help%,%BD%\\%${curDirVar}%\\%work%,%BD%\\%${curDirVar}%\\%busy%,%BD%\\%${curDirVar}%\\%cross%,%BD%\\%${curDirVar}%\\%text%,%BD%\\%${curDirVar}%\\%handwriting%,%BD%\\%${curDirVar}%\\%unavailable%,%BD%\\%${curDirVar}%\\%vert%,%BD%\\%${curDirVar}%\\%horz%,%BD%\\%${curDirVar}%\\%dgn1%,%BD%\\%${curDirVar}%\\%dgn2%,%BD%\\%${curDirVar}%\\%move%,%BD%\\%${curDirVar}%\\%alternate%,%BD%\\%${curDirVar}%\\%link%,%BD%\\%${curDirVar}%\\%pin%,%BD%\\%${curDirVar}%\\%person%\""
    setupInfo.strings << "${curDirVar}            = \"Cursors\\${curDir}\"\n" +
                         "${schemeNameVar}        = \"${name}\""
}

def winCurDir(Map args) {
    def tags = [ args.shape, args.color, args.leftHand, args.outline, args.shadow, args.sizing] - null
    return "Bibata\\${ tags.join('-') }"
}

task linuxThemeFiles {
    description = 'index.theme, cursor.theme'
    group = 'package'

    doLast {
        ['Classic', 'Ice'].each { color ->
            ['Modern', 'Original'].each { shape ->
                def args = [ shape: shape,
                             color: color,
                             leftHand: null,
                             outline: null,
                             shadow: null,
                             sizing: null ]

                writeLinuxThemeFiles(args)
                writeLinuxThemeFiles(args << [outline: 'Thin'])
                writeLinuxThemeFiles(args << [shadow: 'Shadow'])
                writeLinuxThemeFiles(args << [outline: null])
            }
        }
    }
}

def writeLinuxThemeFiles(Map args) {
    def tags = [ args.shape, args.color,
                 args.leftHand, args.outline, args.shadow ] - null
    def properties = [
        name: themeName(args),
        comment: themeComment(args),
        version: cursorsVersion,
        curDir: 'Bibata-' + tags.join('-')
    ]

    copy {
        into "${buildDir}/linux-themes/${properties.curDir}"
        from('src/package') {
            include 'index.theme'
            include 'cursor.theme'
        }
        expand(properties) {
            escapeBackslash = true
        }
    }
}
