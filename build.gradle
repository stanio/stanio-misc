plugins {
    id 'java-library'
    id 'eclipse'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.release = 17
}

tasks.named('test') {
    useJUnitPlatform()
}

javadoc {
    options.encoding = 'UTF-8'
    options.addBooleanOption('Xdoclint:-missing', true)
    options.addStringOption('-release', '17')
}

eclipse {
    classpath {
        def baseOutputDir = '.classes'
        defaultOutputDir = file("${baseOutputDir}/default")

        // This is ridiculous:
        // https://github.com/gradle/gradle/issues/3839
        file.whenMerged {
            entries.each { entry ->
                if (entry instanceof org.gradle.plugins.ide.eclipse.model.SourceFolder) {
                    entry.output = entry.output.replaceFirst('^bin(?=/)', baseOutputDir)
                }
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation('org.apache.xmlgraphics:batik-transcoder:1.17') {
        exclude group: 'xml-apis', module: 'xml-apis'
    }
    implementation('org.apache.xmlgraphics:batik-codec:1.17') {
        exclude group: 'xml-apis', module: 'xml-apis'
    }
    implementation 'com.google.code.gson:gson:2.10.1'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
    testRuntimeOnly    'org.junit.platform:junit-platform-launcher'
}
